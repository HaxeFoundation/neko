# Detect if the install is run by CPack.
if (${CMAKE_INSTALL_PREFIX} MATCHES "/_CPack_Packages/.*/(TGZ|ZIP)/")
	# Flatten the directory structure such that everything except the header files is placed in root.
	if (${CMAKE_VERSION} VERSION_LESS 3.3)
		file(GLOB bin_files ${CMAKE_INSTALL_PREFIX}/bin/*)
		file(GLOB lib_files ${CMAKE_INSTALL_PREFIX}/lib/* ${CMAKE_INSTALL_PREFIX}/lib/**/*)
		foreach(file ${bin_files} ${lib_files})
			if (NOT IS_DIRECTORY ${file})
				get_filename_component(file_name ${file} NAME)
				execute_process(
					COMMAND ${CMAKE_COMMAND} -E rename
					${file}
					${CMAKE_INSTALL_PREFIX}/${file_name}
				)
			endif()
		endforeach()
	else()
		file(GLOB bin_files LIST_DIRECTORIES FALSE ${CMAKE_INSTALL_PREFIX}/bin/*)
		file(GLOB lib_files LIST_DIRECTORIES FALSE ${CMAKE_INSTALL_PREFIX}/lib/* ${CMAKE_INSTALL_PREFIX}/lib/**/*)
		foreach(file ${bin_files} ${lib_files})
			get_filename_component(file_name ${file} NAME)
			execute_process(
				COMMAND ${CMAKE_COMMAND} -E rename
				${file}
				${CMAKE_INSTALL_PREFIX}/${file_name}
			)
		endforeach()
	endif()
	execute_process( COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_INSTALL_PREFIX}/bin)
	execute_process( COMMAND ${CMAKE_COMMAND} -E remove_directory ${CMAKE_INSTALL_PREFIX}/lib)

	# install additional files
	file (
		INSTALL
			@CMAKE_SOURCE_DIR@/README.md
			@CMAKE_SOURCE_DIR@/LICENSE
			@CMAKE_SOURCE_DIR@/CHANGES
		DESTINATION ${CMAKE_INSTALL_PREFIX}
	)
endif()
